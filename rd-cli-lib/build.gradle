plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'org.rundeck.cli'

sourceCompatibility = 1.8

dependencies {

    api project(":rd-api-client")
    api "org.rundeck.cli-toolbelt:toolbelt:$toolbeltVersion"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation "org.codehaus.groovy:groovy-all:2.5.7"
    testImplementation "org.spockframework:spock-core:1.3-groovy-2.5"
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        rdCliLibPublication(MavenPublication) {
            from components.java
            pom {

                description=project.description ?: 'Rundeck CLI Tool Extension Library'
                name= "Rundeck CLI Tool Extension Library ${project.version}"
                url= 'https://github.com/rundeck/rundeck-cli'
                licenses {
                    license {
                        name= 'The Apache Software License, Version 2.0'
                        url ='http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution= 'repo'
                    }
                }
                scm {
                    url ='https://github.com/rundeck/rundeck-cli'
                    connection= 'scm:git:git@github.com/rundeck/rundeck-cli.git'
                    developerConnection= 'scm:git:git@github.com:rundeck/rundeck-cli.git'
                }
                developers {
                    developer {
                        id='gschueler'
                        name='Greg Schueler'
                        email='greg@rundeck.com'
                    }
                }
            }

        }
    }
}

def bintrayUser = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
def bintrayKey = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') :
                 System.getenv('BINTRAY_API_KEY')
def bintrayUserOrg = project.hasProperty('bintrayUserOrg') ? project.property('bintrayUserOrg') :
                     System.getenv('BINTRAY_USER_ORG')
def bintrayRepo = project.hasProperty('bintrayRepo') ? project.property('bintrayRepo') :
                  System.getenv('BINTRAY_REPO')

def component = (
    project.hasProperty('component') ? project.property('component') :
    System.getenv('BINTRAY_UPLOAD_COMPONENT')
)
if (component &&  ['cli-lib'].contains(component) && bintrayUser && bintrayKey && bintrayRepo) {

    apply plugin: 'com.jfrog.bintray'
    bintray {
        user = bintrayUser
        key = bintrayKey
        publications = ['rdCliLibPublication']

        dryRun = false
        publish = true
        override = true
        pkg {
            name = project.name
            repo = bintrayRepo
            if (bintrayUserOrg) {
                userOrg = bintrayUserOrg
            }
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/rundeck/rundeck-cli.git'
            websiteUrl = 'https://github.com/rundeck/rundeck-cli'
            issueTrackerUrl = 'https://github.com/rundeck/rundeck-cli/issues'
            githubRepo = 'rundeck/rundeck-cli' //Optional Github repository
            githubReleaseNotesFile = 'README.md' //Optional Github readme file

            version {
                name = rpmVersion
                desc = "${description} ${rpmVersion}"
                vcsTag = "v${rpmVersion}"
            }
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = System.getenv('SIGNING_PASSWORD')
            }
        }
    }
}
