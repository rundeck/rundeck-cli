/**
 * Define project extension values in the project gradle file before including this file:
 *
 * publishName = 'Name of Package'
 * publishDescription = 'description' (optional)
 * publishShadow = true/false , to publish shadow jar (optional, default false)
 * githubSlug = Github slug e.g. 'rundeck/rundeck-cli'
 * developers = [ [id:'id', name:'name', email: 'email' ] ] list of developers
 *
 * Define project properties to sign and publish when invoking publish task:
 *
 *     ./gradlew \
 *     -PsigningKey="base64 encoded gpg key" \
 *     -PsigningPassword="password for key" \
 *     -PsonatypeUsername="sonatype token user" \
 *     -PsonatypePassword="sonatype token password" \
 *     publishToSonatype closeAndReleaseSonatypeStagingRepository
 */
apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    publications {
        "${project.name}"(MavenPublication) { publication ->
            if (ext.hasProperty('publishShadow') && ext.publishShadow) {
                project.shadow.component(publication)
            } else {
                from components.java
            }

            pom {
                name = publishName
                description = ext.hasProperty('publishDescription') ? ext.publishDescription :
                              project.description ?: publishName
                url = "https://github.com/${githubSlug}"
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = "https://github.com/${githubSlug}"
                    connection = "scm:git:git@github.com/${githubSlug}.git"
                    developerConnection = "scm:git:git@github.com:${githubSlug}.git"
                }
                if (ext.hasProperty('developers')) {
                    developers {
                        ext.developers.each { dev ->
                            developer {
                                id = dev.id
                                name = dev.name
                                email = dev.email
                            }
                        }
                    }
                }
            }

        }
    }
}
def base64Decode = { String prop ->
    project.findProperty(prop) ?
    new String(Base64.getDecoder().decode(project.findProperty(prop).toString())).trim() :
    null
}

if (project.hasProperty('signingKey') && project.hasProperty('signingPassword')) {
    signing {
        useInMemoryPgpKeys(base64Decode("signingKey"), project.signingPassword)
        sign(publishing.publications)
    }
}
